def srcDir = '../'
def dstDir = '../'

if (project.hasProperty('sourceDir')) {
  println 'setting default sourceDir'
  srcDir = sourceDir
}
if (project.hasProperty('destDir')) {
  println 'setting default destDir'  
  dstDir = destDir
}

// We now setup the jsweet transpiler plugin for gradle
// Documentation at https://github.com/lgrignon/jsweet-gradle-plugin
buildscript {
  repositories {
    mavenCentral()
    maven { url "http://repository.jsweet.org/artifactory/libs-release-local" }
    maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }
    maven { url "http://repository.jsweet.org/artifactory/plugins-release-local" }
    maven { url "http://repository.jsweet.org/artifactory/plugins-snapshot-local" }
    maven { url "http://google-diff-match-patch.googlecode.com/svn/trunk/maven" }
  }
  dependencies {
    classpath('org.jsweet:jsweet-gradle-plugin:1.2.1-SNAPSHOT') { //
      transitive = true }
  }
}

apply plugin: 'java'
apply plugin: 'org.jsweet.jsweet-gradle-plugin'

repositories {
  mavenCentral()
  maven { url "http://repository.jsweet.org/artifactory/libs-release-local" }
  maven { url "http://repository.jsweet.org/artifactory/libs-snapshot-local" }

  maven { url "http://repository.jsweet.org/artifactory/plugins-release-local" }
  maven { url "http://repository.jsweet.org/artifactory/plugins-snapshot-local" }
  maven { url "http://google-diff-match-patch.googlecode.com/svn/trunk/maven" }
}

// No need to compile java, since this is BlueJ's job
compileJava {
  enabled = false
}

dependencies {
  compile group: 'org.jsweet.candies', name: 'jsweet-core', version:'1.2.0-SNAPSHOT'
  compile group: 'org.jsweet.candies', name: 'j4ts', version:'0.2.0-SNAPSHOT'
  compile group: 'org.jsweet', name: 'jsweet-transpiler', version:'1.1.1-SNAPSHOT'

  compile fileTree(include: ['*.jar'], dir: 'libs')
}

def scenecreator = false;
task copyFromEnv() {
  doLast {
    delete fileTree(dir: './src/main/java/', include: '**/*.java')
    copy {
      println 'copying srouce '+srcDir
      from srcDir
      into './src/main/java/'
      include "*.java"
      def line
      new File(srcDir+'Game.java').withReader { line = it.readLine() }
      if (line.startsWith('//scenecreator auto-generated')) {
        println 'scenecreator world detected'
        scenecreator = true;
        include "world.txt"
        exclude "Game.java"
      }
    }
  }
}

task dist() {
  doLast {
    copy {
      println 'copying bundle.js'
      from 'target/js/'
      into dstDir+"js/"
      include 'bundle.js*'
    }
    if (new File('src/main/java/world.txt').exists()) {
      copy {
        println 'copying world.txt'
        from 'src/main/java/'
        into dstDir
        include 'world.txt'
      }
    }
    if (scenecreator) {
      java.awt.Desktop.desktop.browse ( new File(dstDir+'scene.html').toURI());
    } else {
      java.awt.Desktop.desktop.browse ( new File(dstDir+'game.html').toURI());      
    }
  }
}

task stripMain() {
  doLast {
    println 'stripping main call'
    def gamejs = new File('target/js/game.js');
    new File('target/js/bundle.js').eachLine {
      line ->
        if (!(line =~ /.*\.main\(null\).*/)) {
          gamejs << line + '\n';
        }
    }
    copy {
      from 'target/js'
      into 'target/js'
      rename('game.js','bundle.js')
    }
  }
}

jsweet {
  verbose = true
  encoding = 'UTF-8'
  sourceMap = true
  outDir = new File('target/js')
  targetVersion = 'ES3'
  includes = ['**/*.java']
  bundle = true
}

// Sequence of tasks -- main task is 'dist'
copyFromEnv.dependsOn jsweetClean
tasks.jsweet.dependsOn copyFromEnv
stripMain.dependsOn tasks.jsweet
dist.dependsOn stripMain
